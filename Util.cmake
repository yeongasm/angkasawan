# taken from https://stackoverflow.com/a/31423421
# Purpose: Tells VS filters to mimic folder structure
function(assign_source_group)
    foreach(_source IN ITEMS ${ARGN})
        if (IS_ABSOLUTE "${_source}")
            file(RELATIVE_PATH _source_rel "${CMAKE_CURRENT_SOURCE_DIR}" "${_source}")
        else()
            set(_source_rel "${_source}")
        endif()
        get_filename_component(_source_path "${_source_rel}" PATH)
        string(REPLACE "/" "\\" _source_path_msvc "${_source_path}")
        source_group("${_source_path_msvc}" FILES "${_source}")
    endforeach()
endfunction(assign_source_group)

# treat warnings as errors
function(warnings_as_errors project)
    if (MSVC)
        target_compile_options(${project} PRIVATE /W4 /WX)
    else()
        target_compile_options(${project} PRIVATE -Wall -Wextra -pedantic -Werror)
    endif()
endfunction(warnings_as_errors)

function(disable_rtti_and_exceptions project)
    if (MSVC)
        target_compile_options(${project} PRIVATE /GR- /EHS-c-)
    else()
        target_compile_options(${project} PRIVATE -fno-rtti -fno-exceptions)
    endif()
endfunction()